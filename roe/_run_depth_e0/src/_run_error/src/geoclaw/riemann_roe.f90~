subroutine shallow_roe(hL,hR,huL,huR,sw,fw)
	!note: this subroutine assumes that the depth of both cells is sufficient
	!If hL or hR are too close to zero, bad stuff will happen.
	
	!input
	double precision, intent(in) :: hL,hR,huL,huR
	
	!output
	double precision, intent(out) :: sw(2),fw(2,2)!arg1: component, arg2:wave
	
	
	!local
	double precision :: uL,uR,deltah,deltahu
	double precision :: uhat,chat,s1,s2,a1,a2
	
	!compute velocities
	uL=huL/hL
	uR=huR/hR
	
	!roe averaged quantities
	uhat=(sqrt(hL)*uL+sqrt(hR)*uR)/(sqrt(hL)+sqrt(hR))
	chat=sqrt(g*0.5d0*(hL+hR))
	
	!wave speeds
	s1=uhat-chat
	s2=uhat+chat
	
	!decompose jump vector
	deltah=hR-hL
	deltahu=huR-huL
	a1=0.5d0*(-deltahu+s2*deltah)/chat
	a2=0.5d0*( deltahu-s1*deltah)/chat
	
	!fwaves
	fw(1,1)=s1*a1
	fw(2,1)=s1*a1*(uhat-chat)
	fw(1,2)=s2*a2
	fw(2,2)=s2*a2*(uhat+chat)
	sw(1)=s1
	sw(2)=s2
	
	
	
	return
end subroutine shallow_roe
